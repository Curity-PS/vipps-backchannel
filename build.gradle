import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
    id 'groovy'
    id "io.curity.gradle.curity-plugin-dev" version "0.3.0"
}

group = 'io.curity.identityserver.plugin'
version = project.hasProperty('version') ? project.property('version') : '0.1.0'

repositories {
    mavenLocal()
    maven {
        url 'https://maven.pkg.github.com/curity-ps/curity-ps-sdk-commons'
        credentials {
            username = System.getenv('GITHUB_ACTOR') ?: project.findProperty('gpr.user')
            password = System.getenv('GITHUB_TOKEN') ?: project.findProperty('gpr.token')
        }
    }
    mavenCentral()
}

dependencies {
    compileOnly 'se.curity.identityserver:identityserver.sdk:10.7.1'
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib:2.0.0'
    compileOnly 'org.slf4j:slf4j-api:2.0.12'
    compileOnly 'jakarta.validation:jakarta.validation-api:3.0.0'
    implementation 'io.curity:curity-ps-sdk-commons:0.3.1'

    // Test dependencies
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.apache.groovy:groovy-all:4.0.15'
    testImplementation 'se.curity.identityserver:identityserver.sdk:10.7.1'
    testImplementation 'org.slf4j:slf4j-api:2.0.12'
    testImplementation 'jakarta.validation:jakarta.validation-api:3.0.0'
    testImplementation 'io.curity:curity-ps-sdk-commons:0.3.1:tests'

    // Testcontainers for integration tests
    testImplementation 'org.testcontainers:testcontainers:2.0.3'
    testImplementation 'org.testcontainers:testcontainers-spock:2.0.3'

    // WireMock for mocking external services
    testImplementation 'org.wiremock:wiremock:3.12.0'

    // JWT signing for mock ID tokens
    testImplementation 'com.nimbusds:nimbus-jose-jwt:10.3'

    // SLF4J simple logger for test output (outputs container logs to stdout)
    testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.12'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

kotlin {
    jvmToolchain(21)
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '21'
    }
}

test {
    useJUnitPlatform()
}